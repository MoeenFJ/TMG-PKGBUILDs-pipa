#Maintainer: Raihan Ahamed (raihan2000) <raihan1999ahamed@gmail.com>

pkgname=(hexagonrpcd)
pkgdesc="Qualcomm HexagonFS daemon"
pkgver=0.3.2.r0.gb7eff23
pkgrel=2
arch=(any)
license=(GPL-3.0-or-later)
url="https://gitlab.com/sdm845-mainline/hexagonrpc/"
depends=(glibc)
makedepends=(
    linux-headers
    meson
    git
)
_commit=b7eff23db1b11541d11175bd3f42bd4c85214baf  # tags/0.3.2^0
source=(
    "git+https://gitlab.com/sdm670-mainline/hexagonrpc.git#commit=$_commit"
    10-fastrpc.rules
    hexagonrpcd-adsp-rootpd.service
    hexagonrpcd-adsp-sensorspd.service
    hexagonrpcd-sdsp.service
)
sha256sums=(
    58ad8305f9cc06885dd2e589ed8637b1b8428f6930c7f36001b6b4570cfabd81
    41dfc4e8c4fd88f461a5a6e4a4e86849eb302f8bcb2b5ce2efa9690f5415d52d
    571ac3582402e31db573177f711af094a3ff25e17c0e40872c79cff28acb13ac
    c72195c102dde85ef0ed5361d7a88051680c2f05229a5fa3d5aa87bc89435288
    922b03ae73967e7a7103623e2e888672312093bad86b8f3ca200234d660ef4be
)

pkgver() {
  cd hexagonrpc
  git describe --long --abbrev=7 --tags $_commit | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson hexagonrpc build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install --no-rebuild -C build

  echo -e 'g fastrpc\nu fastrpc - "Qualcomm HexagonFS service" /var/lib/fastrpc' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/fastrpc.conf"

  # Allow access for FastRPC node for FastRPC user/group
  install -Dm 644 "$srcdir"/10-fastrpc.rules -t "$pkgdir"/usr/lib/udev/rules.d/

  # Add Services
  install -Dm755 "$srcdir"/$pkgname-adsp-rootpd.service "$pkgdir"/usr/lib/systemd/system/$pkgname-adsp-rootpd.service
  install -Dm755 "$srcdir"/$pkgname-adsp-sensorspd.service "$pkgdir"/usr/lib/systemd/system/$pkgname-adsp-sensorspd.service
  install -Dm755 "$srcdir"/$pkgname-sdsp.service "$pkgdir"/usr/lib/systemd/system/$pkgname-sdsp.service

  # Enable hexagonrpcd-sdsp by default, its needed for iio-sensor-proxy
  install -d "$pkgdir/etc/systemd/system/multi-user.target.wants"

  ln -sf /usr/lib/systemd/system/hexagonrpcd-sdsp.service \
      "$pkgdir/etc/systemd/system/multi-user.target.wants/hexagonrpcd-sdsp.service"
}
